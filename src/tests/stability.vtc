varnishtest "Test querysort.querysort sort stability"

server s1 -repeat 6 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import querysort;

	sub vcl_deliver {
		set resp.http.url = querysort.querysort(req.http.url);
	}
} -start

client c1 {
	txreq -hdr "url: /w/api.php?action=parse&page=Varnish&prop=wikitext&prop=parsetree&format=json"
	rxresp
	expect resp.http.url == "/w/api.php?action=parse&format=json&page=Varnish&prop=wikitext&prop=parsetree"

	delay .1

	txreq -hdr "url: /search-redirect.php?family=Wikipedia&language=en&search=Varnish&language=de&go=Go"
	rxresp
	expect resp.http.url == "/search-redirect.php?family=Wikipedia&go=Go&language=en&language=de&search=Varnish"

	delay .1

	txreq -hdr "url: /foo?foo[]=c&foo[]=b&foo[0]=a"
	rxresp
	expect resp.http.url == "/foo?foo[]=c&foo[]=b&foo[0]=a"

	delay .1

	txreq -hdr "url: /w/index.php?title=Varnish&action=edit&preloadparams[]=c&preloadparams[0]=b&preloadparams[]=a"
	rxresp
	expect resp.http.url == "/w/index.php?action=edit&preloadparams[]=c&preloadparams[0]=b&preloadparams[]=a&title=Varnish"

	delay .1

	txreq -hdr "url: /w/index.php?title=Varnish&action=edit&preloadparams%5B%5D=c&preloadparams[0]=b&preloadparams[]=a"
	rxresp
	expect resp.http.url == "/w/index.php?action=edit&preloadparams%5B%5D=c&preloadparams[0]=b&preloadparams[]=a&title=Varnish"

	delay .1

	txreq -hdr "url: /blah?foo[]=a&foo%5B%5D=b&fooa=q&foo=c&foo%5b%5d=d"
	rxresp
	expect resp.http.url == "/blah?foo[]=a&foo%5B%5D=b&foo=c&foo%5b%5d=d&fooa=q"
} -run
